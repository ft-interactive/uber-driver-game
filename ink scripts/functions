=== function add_time(hours, mins)===

 ~ timestamp = timestamp + (hours * 3600) + (mins * 60)
 ~ return timestamp

=== function alter(ref x, k) ===
    ~ x += k

=== function time_passes(hours, peak, mult)  ===
~ temp rides=0
~ temp fares=0
~ temp randomised_minutes=RANDOM(0,20)-10
~ temp avg_hourly_fare=15.38
~ temp rating_change=RANDOM(0,10)-5
// avg_fare_per_ride=6.83

~ time_passing = true

{ peak==1: 
~ rides = RANDOM((hours*2),(hours*4))
    {current_city=="sac":
    ~ alter(rides, -2)
    }
~ fares = rides*RANDOM(5,8)*1.25

- else:
~ rides = RANDOM((hours*2),(hours*3))
    {current_city=="sac":
    ~ alter(rides, -2)
        {rides<0:
        ~rides=0
        }
    }
~ fares = rides*RANDOM(5,8)
}

~ fares = fares * mult
~ alter(day_ride_count, rides)
~ alter(day_fares_earned, fares)
~ alter(day_hours_driven, hours)
~ alter(quest_rides, -rides)
~ alter(ride_count_total, rides)
~ alter(fares_earned_total, fares)
~ alter(hours_driven_total, hours)

~ add_time(hours, randomised_minutes)

~ alter(rating, rating_change)
{rating>500:
~rating=500
}

{quest_rides<0: 
~quest_rides=0
}
{quest_rides<1:
~quest_completion=true
~moments=true
}
/*
NEXT SCREEN: You drive for {hours} hour{hours>1:s}. During this time, you completed {rides} rides, and earned ${fares} in fares.  {quest_rides > 0: You still need {quest_rides} more ride{quest_rides>1:s} to get the bonus.}
*/


=== function day_end ===

Today, you drove for {day_hours_driven} hours, completed {day_ride_count} rides and earned $[[[{day_fares_earned}]]] in fares. /* Your driver rating is {rating/100}. */

{quest_rides>0:
You still need {quest_rides} ride{quest_rides>1:s} before {day_5_start:the end of Sunday | Friday} to get the bonus.
}

~ day_ride_count=0
~ day_fares_earned=0
~ day_hours_driven=0

=== function UberXL ===
~ temp XL_bonus= RANDOM(12,40)
{car=="minivan": 
    You also got a few UberXL rides that earned you an extra ${XL_bonus}
    ~ alter(day_fares_earned,XL_bonus)
    ~ alter(fares_earned_total, XL_bonus)
    ~ alter(XL_total,XL_bonus)
}

=== function add_ride(number) ===
~alter(ride_count_total,number)
~alter(day_ride_count,number)
~alter(quest_rides,-number)
